plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.ureca'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // ----- Spring Boot Starters -----
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    testImplementation 'io.projectreactor:reactor-test'

    // ----- Spring ai AZURE-OpenAI -----
    implementation platform("org.springframework.ai:spring-ai-bom:1.0.1")
    implementation 'org.springframework.ai:spring-ai-starter-model-azure-openai'

    // ----- AWS / SNS -----
    implementation platform("io.awspring.cloud:spring-cloud-aws-dependencies:3.0.1")
    implementation 'io.awspring.cloud:spring-cloud-aws-starter-sns'

    // ----- email -----
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: '3.0.5'

    // ----- Redis & AMQP -----
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation "org.redisson:redisson-spring-boot-starter:3.17.6"

    // ----- Redis & AMQP -----
    implementation "net.javacrumbs.shedlock:shedlock-spring:6.9.2"
    implementation "net.javacrumbs.shedlock:shedlock-provider-redis-spring:6.9.2"

    // ----- QueryDSL -----
    implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"

    // ----- JWT (jjwt) -----
    implementation "io.jsonwebtoken:jjwt-api:0.12.6"
    runtimeOnly "io.jsonwebtoken:jjwt-impl:0.12.6"
    runtimeOnly "io.jsonwebtoken:jjwt-jackson:0.12.6"

    // ----- Jackson Date/Time -----
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // ----- Lombok -----
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // ----- JDBC Drivers -----
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'com.mysql:mysql-connector-j'

    runtimeOnly 'io.netty:netty-resolver-dns-native-macos:4.1.115.Final:osx-aarch_64'

    // ----- Testing -----
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'com.h2database:h2'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // testContainers 모듈 버전 관리
    testImplementation "org.testcontainers:testcontainers:2.0.2"
    testImplementation "org.testcontainers:testcontainers-mysql:2.0.2"
    testImplementation "org.testcontainers:testcontainers-rabbitmq:2.0.2"

    // Awaitility (비동기 테스트)
    testImplementation 'org.awaitility:awaitility:4.3.0'

    // s3
    implementation(platform("software.amazon.awssdk:bom:2.25.38"))
    implementation "software.amazon.awssdk:s3"

    // Srping retry
    implementation 'org.springframework.retry:spring-retry'
    implementation 'org.springframework:spring-aspects'
}

tasks.named('test') {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it,
                    // 1. 포함할 도메인만 명시 (Full 대상)
                    includes: [
                            "**/com/ureca/snac/asset/**",
                            "**/com/ureca/snac/infra/**",
                            "**/com/ureca/snac/money/**",
                            "**/com/ureca/snac/outbox/**",
                            "**/com/ureca/snac/payment/**",
                            "**/com/ureca/snac/settlement/**",
                            "**/com/ureca/snac/wallet/**"
                    ],
                    // 2. 포함된 도메인 안에서도 굳이 안 봐도 되는 것들 쳐내기
                    excludes: [
                            "**/dto/**",
                            "**/entity/**",
                            "**/event/**",
                            "**/*Exception*",
                            "**/*Config*",
                            "**/*Mapper*"
                    ]
            )
        }))
    }
    finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'CLASS'

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.00 // 담당 도메인은 80% 사수!
            }

            // 검증에서도 동일한 도메인만 남기기 위해 리포트와 동일한 includes 설정
            // (excludes를 활용해 반대로 쳐내는 것이 관리하기 편함)
            includes = [
                    "**/com/ureca/snac/asset/**",
                    "**/com/ureca/snac/infra/**",
                    "**/com/ureca/snac/money/**",
                    "**/com/ureca/snac/outbox/**",
                    "**/com/ureca/snac/payment/**",
                    "**/com/ureca/snac/settlement/**",
                    "**/com/ureca/snac/wallet/**"
            ]
            excludes = [
                    "**/dto/**",
                    "**/entity/**",
                    "**/event/**",
                    "**/*Exception*",
                    "**/*Config*",
                    "**/*Mapper*"
            ]
        }
    }
}

test {
    finalizedBy 'jacocoTestReport'
    // ...
}